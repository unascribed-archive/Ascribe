buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
		classpath 'me.tatarka:gradle-retrolambda:3.2.0'
	}
}

plugins {
	id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'me.tatarka.retrolambda'

group = "com.unascribed"
archivesBaseName = "Ascribe"
version = "1.8.9-0.1.1"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	compile 'com.github.gfx.util:weak-identity-hash-map:2.0.1'
	shadow 'com.github.gfx.util:weak-identity-hash-map:2.0.1'
}

retrolambda {
	jdk "/usr/lib/jvm/java-8-oracle"
	oldJdk "/usr/lib/jvm/java-6-openjdk-amd64"
	javaVersion JavaVersion.VERSION_1_6
	defaultMethods true
	
}

jar {
	manifest {
		attributes (
			'FMLAT': 'ascribe_at.cfg'
		)
	}
	classifier = 'slim'
}


shadowJar {
	classifier = ''
	relocate 'com.github.gfx.util', "com.unascribed.ascribe.repackage.com.github.gfx.util"
}

reobf {
	shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
	archives shadowJar
}

minecraft {
	version = "1.8.9-11.15.1.1761"
	mappings = "snapshot_20160224"
	
	runDir = "eclipse"
	
	replaceIn "src/main/java/com/unascribed/ascribe/Ascribe.java"
	replace "@VERSION@", project.version
	replace "@MCVERSION@", project.minecraft.version
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
				
		// replace version and mcversion
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
		
	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}
